

&Вместо("ОбъектМодифицированДляПланаОбменаПопыткаИсключение")
Функция МО_ОбъектМодифицированДляПланаОбменаПопыткаИсключение(Источник, ОбъектМетаданных, ИмяПланаОбмена, РежимЗаписи, ЗарегистрироватьОбъектКВыгрузке)
	
	//{[-](фрагмент удален)SAP 03.11.2017 10:49:32
	// Если ЗарегистрироватьОбъектКВыгрузке Или Источник.ЭтоНовый() Или Источник.ОбменДанными.Загрузка Тогда
	//}SAP 03.11.2017 10:49:32
	//{[+](фрагмент добавлен), SAP 03.11.2017 10:50:01
	Если ЗарегистрироватьОбъектКВыгрузке Или Источник.ЭтоНовый() Или Источник.ОбменДанными.Загрузка Или ТипЗнч(Источник) = Тип("ДокументОбъект.ОперацияБух") Тогда
	//}SAP 03.11.2017 10:50:01
	
		// Регистрируем изменения всегда
		// - для наборов записей регистров,
		// - при физическом удалении объектов,
		// - для новых объектов,
		// - для объектов записанных по обмену данными.
		Возврат Истина;
		
	ИначеЕсли РежимЗаписи <> Неопределено И ИзмененоПроведениеДокумента(Источник, РежимЗаписи) Тогда
		// Если изменен признак документа "Проведен", то считаем документ измененным.
		Возврат Истина;
		
	КонецЕсли;
	
	ИмяОбъекта = ОбъектМетаданных.ПолноеИмя();
	
	ТаблицаРеквизитовРегистрации = ОбменДаннымиПовтИсп.РеквизитыОбъектаДляРегистрации(ИмяОбъекта, ИмяПланаОбмена);
	
	Если ТаблицаРеквизитовРегистрации.Количество() = 0 Тогда
		// Если правила ВРО не заданы, то считаем, что фильтра ВРО нет.
		// Объект модифицирован всегда.
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицыРеквизитовРегистрации Из ТаблицаРеквизитовРегистрации Цикл
		
		ЕстьИзмененияВерсийОбъектов = ОпределитьИзмененияВерсийОбъекта(Источник, СтрокаТаблицыРеквизитовРегистрации);
		
		Если ЕстьИзмененияВерсийОбъектов Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	// Если дошли до конца, то объект не изменился по реквизитам регистрации. Регистрация на узлах не нужна.
	Возврат Ложь;
КонецФункции
